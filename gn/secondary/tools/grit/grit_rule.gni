# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("grit") {
  assert(defined(source),
         "\"source\" must be defined for the grit template $target_name")
  assert(!defined(sources) && !defined(outputs),
         "Neither \"sources\" nor \"outputs\" can be defined for the grit " +
         "template $target_name")

  resource_ids = to_build_path("//tools/gritsettings/resource_ids")
  output_dir = to_build_path(target_gen_dir)
  grit_info_script = to_build_path("//tools/grit/grit_info.py")
  source_path = to_build_path(source)

  grit_inputs = exec_script(grit_info_script,
    [ "--inputs", source_path, "-f", resource_ids], "list lines")
  grit_outputs = exec_script(grit_info_script,
    [ "--outputs", "$output_dir", source_path, "-f", resource_ids ],
    "list lines")

  # The current grit setup makes an file in $target_gen_dir/grit/foo.h that
  # the source code expects to include via "grit/foo.h". It would be nice to
  # change this to including absolute paths relative to the root gen directory
  # (like "mycomponent/foo.h"). This config sets up the include path.
  grit_config = target_name + "_grit_config"
  config(grit_config) {
    includes = [ target_gen_dir ]
  }

  grit_custom_target = target_name + "_grit"
  custom(grit_custom_target) {
    script = "//tools/grit/grit.py"
    source_prereqs = grit_inputs
    outputs = grit_outputs

    # TODO(brettw) grit_defines.
    args = [
      "-i", source_path, "build",
      "-f", resource_ids,
      "-o", output_dir,
    ]
  }

  # This is the thing that people actually link with, it must be named the
  # same as the argument the template was invoked with.
  static_library(target_name) {
    # Since we generate a file, we need to be run before the targets that
    # depend on us.
    hard_dep = true

    sources = grit_outputs
    deps = [ ":$grit_custom_target" ]
    direct_dependent_configs = [ ":$grit_config" ]
  }
}
